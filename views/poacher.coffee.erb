# Ruby rand
rand: (n) ->
  Math.floor(Math.random()*n)

Array.prototype.pick: ->
  this[rand(@length)]

window.Art: Art: {
  width: 960,
  height: 1280,
  visibleWidth: 700
  visibleHeight: 320
  works: []
  add: (fn) ->
    @works.push fn
  colors: ['#FFFFFF', '#9900CC', '#0066CC', '#FFFF00', '#33CC99', '#FF6633']
  draw: (paper) ->
    @bg: paper.rect(0,0,@width,@height).attr({
      fill: @colors.pick()
      stroke: 'none'
      opacity: Math.random()
    }) if rand(5) == 0
    focus: {
      x: (@width-@visibleWidth)*0.5 + rand(@visibleWidth)
      y: (@height-@visibleHeight)*0.5 + rand(@visibleHeight)
    }
    for i in [0...4+rand(4)]
      @works[rand(@works.length)].call(paper, @colors.pick(), focus)
}

# Forces evaluation
for tag in document.getElementsByTagName('script') when tag.type is 'text/x-artwork'
  eval(CoffeeScript.compile(tag.innerHTML))

$(document).ready ->
  frame: $('#frame')
  window.paper: paper: Raphael 'art', Art.width, Art.height
  $(paper.canvas).css('marginTop', -(Art.height - frame.height()) * 0.5)
  Art.draw paper

  frame.find('a.reload').click ->
    paper.clear()
    Art.draw paper
    false

  $('#buy').click ->
    $.ajax {
      type: 'POST'
      url: '/tee'
      data: $('#art')[0].innerHTML.toString()
      contentType: 'image/svg+xml'
      processData: false
      format: 'json'
      success: (data) ->
        console.log data
    }


